#!/usr/bin/env zsh
# vi: ft=zsh

# Source: http://michaelheap.com/pimp-your-zsh/
# More prompt expansion info at http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html

# Fills the color array so that we can do the '%{fg[red]%}'
autoload -U colors && colors
spectrum # Load spectrum function to enable $FG[010] stuff

# Different char for each version control
function prompt_char {
  local color_one="%{$fg[red]%}"
  local color_two="%{$fg[yellow]%}"
  local color_three="%{$fg[green]%}"
  if [[ $platform == 'windows' ]]; then
    local char=">"
  elif [[ "$platform" = 'wsl' ]]; then
    local char=">"
  else
    local char="❯"
  fi
  # git branch >/dev/null 2>/dev/null && echo '±' && return
  # hg root >/dev/null 2>/dev/null && echo '☿' && return
  # echo '$'
  echo "$color_one$char$color_two$char$color_three$char"
}

function git_branch {

  # Check if we're in a git repo
  local git_repo="$(git rev-parse --git-dir 2>/dev/null)"

  if [[ -z "$git_repo" ]]; then
    echo ""
    return -1
  fi

  if [[ "$platform" = 'wsl' ]]; then
    local parens_color="%{$fg[white]%}"
  else
    # This seems to work on everything but the WSL unbuntu :shrug:
    local parens_color="$FG[000]"
  fi
  local left_parens="$parens_color(%{$reset_color%}"
  local right_parens="$parens_color)%{$reset_color%}"

  # Default to everything is fine
  local prompt_color="%{$fg[green]%}"

  # If trimming is needed pipe to: tr -d '[[:space:]]'
  local branch="$(git symbolic-ref --quiet --short HEAD 2>/dev/null)"
  if [[ -n "$branch" ]]; then
    if [[ "$branch" == 'master' ]]; then
      # Our branch is master, set branch color this way
      if [[ -n $(git status --porcelain 2> /dev/null) ]]; then
        # We have uncommited changes
        prompt_color="%{$fg[red]%}"
      elif [[ -n $(git log origin/$branch.. 2> /dev/null) ]]; then
        # We have changes to push
        prompt_color="%{$fg[blue]%}"
      elif [[ -n $(git log master..origin/master 2> /dev/null) ]]; then
        # We haven't pushed our changes
        prompt_color="%{$fg[blue]%}"
      fi
    else
      # We have an actual branch
      if [[ -n $(git status --porcelain 2> /dev/null) ]]; then
        # We have uncommited changes
        prompt_color="%{$fg[red]%}"
      else
        if [[ -n $(git branch -r | grep "origin/$branch") ]]; then
          # We have pushed our branch, check if we've diverged from origin

          if [[ -n $(git log origin/$branch.. 2> /dev/null) ]]; then
            # We have changes to push
            prompt_color="%{$fg[blue]%}"
          fi
        else
          # We have not pushed, check if we've diverged from master

          # TODO: Determine some way to show that we haven't pushed

          if [[ -n $(git log master..$branch 2> /dev/null) ]]; then
            # We've diverged from master
            prompt_color="%{$fg[blue]%}"
          fi
        fi
      fi
    fi

    # TODO: Determine some way to show that we're rebasing
    echo "$left_parens$prompt_color${branch//[^a-zA-Z0-9_\/-]/*}%{$reset_color%}$right_parens"
  else
    # HEAD is not a symbolic ref, meaning we're detached or something
    echo "$left_parens$prompt_color???%{$reset_color%}$right_parens"
  fi
}

function check_exit_code() {
  local last_exit_code=$1
  if [[ $last_exit_code -ne 0 ]]; then
    local dash="%{$fg[red]%}-%{$reset_color%}"
    local center="%{$fg_bold[red]%}$last_exit_code%{$reset_color%}"
    local exit_code_prompt="$dash$center$dash "

    echo "$exit_code_prompt"
  fi
}

function name() {
  # Only show name if it's different than my usual
  local name=""
  local local_name="$(whoami)"

  # Convert name to lowercase, so that TScheffert works too
  if [[ ! "${(L)local_name}" =~ "tscheffe" ]]; then
    name="%n"
  fi

  echo "%{$fg[red]%}$name%{$reset_color%}"
}

function comp_name() {
  local color="%{$fg[green]%}"
  # %m is the TCs-MacBook-Pro-4 (which is my localhost name)
  local name="%m"
  echo "$color$name"
}

function path() {
  local color="%{$fg[yellow]%}"
  # %~ expands to the current working directory but if it starts with $HOME
  #   then replace that with a '~'
  # local dir="%~"
  local dir="$(pwd)"
  # Trim $dir if it's too long
  # TODO: Figure out how to do this generically, other prompts somewhere must do it
  if [[ "$dir" =~ "slalom-configuration-management/" ]]; then
    dir="../${dir#/c/dev/gitlab/slalom-configuration-management/}"
  elif [[ "$dir" =~ "scm/" ]]; then
    dir="../${dir#/c/dev/gitlab/scm/}"
  fi
  local left_bracket="["
  local right_bracket="]"
  echo "$color$left_bracket$dir$right_bracket"
}

function reset_colors() {
  local reset="%{$reset_color%}"
  echo "$reset"
}

function line_number() {
  local color="%{$fg[magenta]%}"
  local ln="%!"
  echo "$color$ln"
}

function precmd() {
  local exit_code=$?
  PROMPT="$(line_number) $(check_exit_code $exit_code)$(name)$(path)$(git_branch) $(prompt_char)$(reset_colors) "
  # RPROMPT="$(git_branch)$(reset_colors)"
}
